module AMM
{
    struct FMA_Location
    {
        long FMAID;
        string name;
    };

    /* Included in AMM namespace to avoid name conflicts in generated software bindings. */
    struct UUID
    {
        string id;
    };

    enum AssessmentValue {
        OMISSION_ERROR
       ,COMMISSION_ERROR
       ,EXECUTION_ERROR
       ,SUCCESS
    };
    struct Assessment
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        UUID id;
        UUID event_id;
        AssessmentValue value;
        string comment;
    };

    enum EventAgentType {
        LEARNER
       ,INSTRUCTOR
       ,SCENARIO
       ,PHYSIOLOGY
    };

    struct EventFragment
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Volatile
         */
    {
        UUID id;
        unsigned long long timestamp;
        UUID educational_encounter;
        FMA_Location location;
        EventAgentType agent_type;
        UUID agent_id;
        string type;
        string data;
    };

    struct EventRecord
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local (In case of module disconnect/reconnect)
         *  Liveliness: Automatic, 1 second lease
         */
    {
        UUID id;
        unsigned long long timestamp;
        UUID educational_encounter;
        FMA_Location location;
        EventAgentType agent_type;
        UUID agent_id;
        string type;
        string data;
    };

    enum FAR_Status {
        REQUESTING
       ,ACCEPTED
       ,REJECTED
    };
    struct FragmentAmendmentRequest
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Volatile
         */
    {
        UUID id;
        UUID fragment_id;
        FAR_Status status;
        // Values that can be amended
        FMA_Location location;
        EventAgentType agent_type;
        UUID agent_id;
    };

    enum LogLevel {
        L_FATAL
       ,L_ERROR
       ,L_WARN
       ,L_INFO
       ,L_DEBUG
       ,L_TRACE
    };
    struct Log
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        unsigned long long timestamp;
        UUID module_id;
        LogLevel level;
        string message;
    };

    struct ModuleConfiguration
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        string name;
        UUID module_id;
        UUID educational_encounter;
        unsigned long long timestamp;
        string capabilities_configuration;
    };

    struct OmittedEvent
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local (In case of module disconnect/reconnect)
         *  Liveliness: Automatic, 1 second lease
         */
    {
        UUID id;
        unsigned long long timestamp;   // When the omission was detected.
        UUID educational_encounter;
        FMA_Location location;
        EventAgentType agent_type;
        UUID agent_id;
        string type;
        string data;
    };

    struct Semantic_Version {
        unsigned short major;
        unsigned short minor;
        unsigned short patch;
    };
    struct OperationalDescription
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        string name;
        string description;
        string manufacturer;
        string model;
        string serial_number;
        UUID module_id;
        string module_version;
       string configuration_version;
       string AMM_version;
        octet ip_address[4];
        string capabilities_schema; // Defined by CapabilitiesSchema.xsd
    };

    struct PhysiologyModification
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        UUID id;
        UUID event_id;
        string type;
        string data;
    };

    struct PhysiologyValue
        /** QoS:
         *  Reliability: Best Effort
         *  Durability: Transient Local
         *  Ownership: Exclusive
         *  Ownership Strength: Set via Configuration if non-zero
         *  Presentation: Access Scope: Instance, Coherent Access: True, Order Access: False
         *  Liveliness: Automatic, 1 second lease
         */
    {
        UUID educational_encounter;
        long long simulation_time;
        unsigned long long timestamp;
        string name;   // BioGears node path
        string unit;
        double value;
    };

    struct PhysiologyWaveform // Reliable delivery
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         *  Ownership: Exclusive
         *  Ownership Strength: Set via Configuration if non-zero
         *  Liveliness: Automatic, 1 second lease (Lower if feasible, requires testing)
         */
    {
        UUID educational_encounter;
        long long simulation_time;
        unsigned long long timestamp;
        string name;   // BioGears node path
        string unit;
        double value;
    };

    struct RenderModification
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         */
    {
        UUID id;
        UUID event_id;
        string type;
        string data;
    };

    enum ControlType {
        RUN
       ,HALT
       ,RESET
       ,SAVE
    };
    struct SimulationControl
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         *  Liveliness: Automatic, 1 second lease
         */
    {
        unsigned long long timestamp;
        ControlType type;
        UUID educational_encounter;
    };

    enum StatusValue {
        OPERATIONAL
       ,INOPERATIVE
       ,EXIGENT
    };
    struct Status
        /** QoS:
         *  Reliability: Reliable
         *  Durability: Transient Local
         *  Liveliness: Automatic, 1 second lease
         */
    {
        UUID module_id;
        string module_name;
        UUID educational_encounter;
        string capability;
        unsigned long long timestamp;
        StatusValue value;
        string message;
    };
};
